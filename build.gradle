apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
version = "1.0-Snapshot"

ext {
    instructionCoverageMin = 0.50f
}

war {
    manifest {
        attributes 'Implementation-Title': 'hibernate-spring-redis-test', 
                   'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/debop/maven"
    }
    jcenter()
}

jacoco {
    toolVersion = "0.7.2.201409121644"
}

jacocoTestReport {
    reports {
	xml.enabled true
    }
}

task checkCoverage(dependsOn: [test, jacocoTestReport]) << {
    def parser = new XmlParser(false, false, true);
    parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false);
    def reportXml = parser.parse("$buildDir/reports/jacoco/test/jacocoTestReport.xml")
    def insCov = reportXml.counter.grep{it.@type == "INSTRUCTION"}[0];
    def insCovPercent = insCov.@covered.toFloat() / (insCov.@missed.toFloat() + insCov.@covered.toFloat())
    if (insCovPercent < instructionCoverageMin) {
    	throw new RuntimeException("Instruction coverage of ${String.format("%.4f", insCovPercent * 100)}% is less than ${String.format("%.4f", instructionCoverageMin * 100)}%")
    }
}

test {
    useTestNG()
    
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/test.exec")    
        classDumpFile = file("$buildDir/build/classes/test")
    }
    
    testLogging.showStandardStreams = true
    
    // listen to events in the test execution lifecycle
    beforeTest { descriptor ->
       logger.lifecycle("Running test: " + descriptor.getName())
    }
    
    // listen to standard out and standard error of the test JVM(s)
    onOutput { descriptor, event ->
       logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message )
    }
}
 
dependencies {
    compile "org.springframework:spring-webmvc:4.1.3.RELEASE"
    compile "org.springframework:spring-orm:4.1.3.RELEASE"
    compile "org.hibernate:hibernate-core:4.3.7.Final"
    compile "com.github.debop:hibernate-redis:1.6.1"
    compile "org.apache.commons:commons-dbcp2:2.0.1"
    compile "mysql:mysql-connector-java:5.1.34"
    compile "com.fasterxml.jackson.core:jackson-databind:2.4.4"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.4.4"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-hibernate4:2.4.4"
    providedCompile "javax.servlet:javax.servlet-api:3.0.1"
    testCompile "org.testng:testng:6.8.8"
    testCompile "org.mockito:mockito-core:1.+"
}
